# Maintainer: Thomas SÃ¤nger <thomas@gecko.space>

_pkgname=GhidraBoy
pkgname=ghidra-extension-$(echo $_pkgname | tr '[:upper:]' '[:lower:]')-git
pkgver=r47.g1c2047a
pkgrel=1
pkgdesc="Sharp SM83 / Game Boy extension for Ghidra"
arch=('x86_64')
url=https://github.com/Gekkio/${_pkgname}
license=('Apache')
provides=()
conflicts=()
depends=('ghidra')
makedepends=('git' 'gradle' 'unzip' 'ghidra')
optdepends=()
source=("$pkgname::git+https://github.com/Gekkio/${_pkgname}")
sha512sums=('SKIP')

prepare() {
  cp -rlfT $srcdir/$pkgname $srcdir/${_pkgname}
}

pkgver() {
  cd $srcdir/${_pkgname}
  count_commits=$(git rev-list --count HEAD)
  commit=$(git rev-parse --short HEAD)
  echo "r${count_commits}.g${commit}"
}

build() {
  cd $srcdir/${_pkgname}
  rm -rf dist

  _ghidra_dir=$(dirname $(readlink -fe $(which ghidra)))

  GHIDRA_INSTALL_DIR=${_ghidra_dir} gradle

  ${_ghidra_dir}/support/sleigh -i ./data/sleighArgs.txt -a ./data/languages
}

package() {
  ghidra_dir=$(dirname $(readlink -fe $(which ghidra)))
  cd $srcdir/${_pkgname}
  
  install -d $pkgdir/${ghidra_dir}/Ghidra/Extensions

  # Extract built archive into destination folder
  unzip -u build/distributions/*.zip -d $pkgdir/${ghidra_dir}/Ghidra/Extensions/

  install -Dm 644 ./data/languages/*.sla $pkgdir/${ghidra_dir}/Ghidra/Extensions/GhidraBoy/data/languages/.

  install -Dm 644 LICENSE -t $pkgdir/usr/share/licenses/$pkgname

  chmod -R a+r $pkgdir
}
